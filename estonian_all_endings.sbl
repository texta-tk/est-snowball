/* Estonian stemmer. 
*/

routines (
	mark_regions
	LONGV
	case_ending
	emphasis
	plural_three_first_cases
	remove_double_kpt
	double
	undouble
	i_plural
	degrees
	substantive
	verb
	nu
)

stringdef a" hex 'E4' //a-umlaut ä
stringdef o" hex 'F6' //o-umlaut ö
stringdef o' hex 'F5' //o with tilde õ
stringdef u" hex 'FC' //u-umlaut ü

externals ( stem )
booleans ( is_verb )
integers ( p1 )
groupings ( V1 RV)
stringescapes {}

define V1 'aeiou{o'}{a"}{o"}{u"}'
define RV 'aeiuo'
define mark_regions as (

    $p1 = limit

    goto V1 gopast non-V1  setmark p1
)


backwardmode (

/*
Miks peab kääne ees olema? EKSS 409 Sest: abivägi, arvamusblogi, mägi, pigi, regi, jõgi. + rongi (gen par), ring, analoog, astroloog, 
EKSS 199 hoki, veski, baski, jänki, katki, krutski, kõmaki, läkiläki. + tüki, püüki, lööki, pinki.


NB! õnnelik-tüüpi (ÕS 19. tüüp) kuulub umbes 1300 sõna.
+ kannike, lapike, pildike, päike, väike, pisike. kassike(ne), musirullikene
lapike, lapikese, lapike-st, lapikese-le, lapikes-te, lapikesi, lapikes-te-le
*/
	define emphasis as (
	    setlimit tomark p1 for ([substring])
            among(
		'gi' (among('ikke' 'sse' 'ike' 'le' 'ni' 'na' 'ta' 'ga' 'de' 'te'))
		'ki' (among('st' 'lt'))
	    )
	    delete //ei saa kätte nom, gen v part-is sõnu nt 'tühjegi', muidu OK. (ˇkatki != kat, slprst käänded)
	)
/*
Verbi tegevusnimed ('mas', 'maks', 'mata', 'mas', 'ma') on kaetud substantiivide all (võrded). Puudu: 'matu'
'des' 'tes' //'es' hakates -> covered in substantive

laulev -> omaette omadussõna juba, ei tee lühemaks.

! lugegu, loetagu, lugegem jne

NB! EI TÖÖTA, KUI EES ÜKS LAHTINE SILP.
*/
	define verb as (
	    setlimit tomark p1 for ([substring])
            among(
		'ksin' 'ksid' 'ksime' 'ksite' //conditional  'ksid' == läheb mitmusega sassi?
		'takse' 'dakse' //impersonal: laul-dakse, luba-takse
		'taks' 'daks' //impersonal conditional: laul-daks, saade-taks
		'akse' //impersonal: tull-akse <- 'a'`````````````` 
		'sime' //pl1pst: saat-sime
		'site' //pl2pst: saat-site
		'vad' (V1) //pl3prs: laula-vad ja laulvad == laula/laul, aga laulvad võiks == laulva?
		'sin' //sg1pst: laul-sin, saat-sin ˇapelsin -> apel :(, laulsin -> laul (sel juhul laulu ains. nim. võrdne lemma) v tõusma -> tõus jne 
		'ime' //pl1pst: tul-ime, teg-ime
		'ite' //pl2pst: teg-ite, jooks-ite ˇministrite, ˇredelite,
		'vat' //prs reference -> luba-vat, laul-vat ++++davat, tavat and lauldava-t vrs laulda-vat :(
		'me' (V1) //pl1prs: laula-me, tule-me
		//sg2pst covered in substantive
		'in' //sg1pst: tul-in
		'is' //sg3pst: saat-is, laul-is     ˇjäätis - jäät, ˇprogrammis;; saatis - saat //s covered in substantive
		//'tagu' 'dagu' //loetagu, lauldagu
		'da' //da-infinitive: luba-da 'a'?????????? lubada , hakata -> ta is covered in substantive
		'n' (V1) //sg1prs: kirjuta-n, aga ˇsaatan == saata, ˇdraakon == draako :( NIMETAV KÄÄNE LÄHEB PEKKI, aga see on igal juhul värd veits.
		//sg2prs is covered covered in substantive 
		'b' (V1) //sg3prs: laula-b

//kümme, võime, tõmme, ilme, pehme, seeme,
//võime, võime, võime-t, võime-le, võime-d, võime-te, võime-id, võime-te-le
//seeme, seemne, seeme-t, seemne-le, seemne-d, seemne-te, seemne-id, seemne-te-le
//ase, aseme, ase-t, aseme-le, aseme-d, aseme-id, aseme-te-sse
//pääse, pääsme, pääse-t, pääsme-le, pääsme-d, pääsme-te, pääsme-id, pääsme-te-le
//84773 me, 23388+9869 sime, 4356 ksime, 154 neg me, 18 nuksime
//1965851+1010400+465668+328529+62418+23013+21133+8516 jne sg g. 
//ge - 18150+8960
//gem - 664 +192 
//gu - 5653+192+930
//tagu - 50+179
	    )
	    delete
	    set is_verb
	)
	define LONGV as 
		among('aa' 'ee' 'ii' 'oo' 'uu' '{a"}{a"}' '{o"}{o"}' '{u"}{u"}' '{o'}{o'}')
	define i_plural as (
	    setlimit tomark p1 for ([substring])
	    among(
		'i' (RV) //raamatui -> raamatu, lapsikui -> lapsiku
	    )
	    delete
	)

	define case_ending as (
	    setlimit tomark p1 for ([substring])
            among(
		'sse' (RV) //illative: saapa-sse
		'st' (RV) //elative: saapa-st kanali-st/kanali-le vrs roheli-st/rohelise-le
//lapike, lapikese, lapike-st, lapikese-le, lapikes-te, lapikesi, lapikes-te-le
//roheline, rohelise, roheli-st, rohelise-le, rohelis-te, rohelisi, rohelis-te-le
		'le' (RV) //allative: raamatu-le
		'lt' (RV) //ablative: raamatu-lt
		'na' (RV) //essive: õpetaja-na
		'ta' //abessiiv and da-infinitive, raamatu-ta and tõus-ta
		'ga' (RV) //komitatiive: õpetaja-ga
		'ks' (RV) //translative: õpetaja-ks
		't' //partitiiv, raamatu-t and kapsas-t
		's'  (RV)//inessive: raamatu-s and kapsa-s //AGA KA MINEVIK? LAULIS??
//jura ˇkursus, aga mõjutab vaid nimetavad, mis on nvn kehvake.
		'l'  (RV) //adessive: raamatu-l and kapsa-l.
//jura ˇahel, ˇaadel ˇministritool, aga mõjutab vaid nimetavad, mis on nvn kehvake

/*
Terminative ending 'ni' is left out as it would worsen the results: there's more 'ni' ending words
in sg g than in ter. 


*/
	    )
	    delete
	)


	define plural_three_first_cases as ( 
	    setlimit tomark p1 for ([substring])
            among(
		'ikkude' (<-'ik') //plural genitive: õnnelikkude -> õnnelik
		'ikke' (<-'ik') //plural partitive: rahulikke -> rahulik
		'ike' (<-'ik') //plural genitive: ohtlike -> ohtlik
		'sid' (not LONGV delete) //plural partitive: auto-sid; exludes plural nominative with words like gaasid, roosid.
		'te' ((test hop 4 delete) or <-'t') //plural genitive and pl2: ministri-te, olulis-te, õun-te and saada-te, laula-te; also torte -> tort (if not in compound word)
		'de' (delete) //plural genitive: lauda-de
		'd' (RV delete) //plural nominative: voodid -> voodi, rattaid -> rattai, lapsikuid -> lapsiku,  ˇmagistrikraad
	    )	    	
	)

	define double as (
            test among('kk' 'tt' 'pp')
	)

	define undouble as (
            next [hop 1] delete
	)

	define nu as (
	    setlimit tomark p1 for ([substring])
            among(
		'nu' //haka-nu(-te-ga)
		'tu' //luba-tu(-d) lubatud, lauldud, tuldud. Siis peaks du ka olema
	    )
	    delete
	)

	define remove_double_kpt as (// undouble consonant if 'C1C1V': mõtte(-le) -> mõte
	    (V1) (double)
	    and undouble
	)

	define degrees as (
	    setlimit tomark p1 for ([substring])
            among(
		'mai' (RV) //heleda-mai(-le)
		'ma' //tugeva-ma(-le) JA sõit-ma, aga eludraa-ma, rukkika-ma
//kui tahaks ainult substantiive ära, siis pane (RV)
		//+ lihtsalt m lõpust ära ka?? heleda-m, tugeva-m (pole vb nii levinud?), aga 'kam-m','bussijaa-m', 'kasu-m'.
	    )
	    delete
	)

	define substantive as (
	    do emphasis
	    do case_ending
	    do plural_three_first_cases
	    do degrees
	    do i_plural
	    do nu
	)
)

define stem as (
	do mark_regions
	unset is_verb
	backwards (
	    do verb
	    try (not is_verb do substantive)
	    do remove_double_kpt
	    
	)
)
