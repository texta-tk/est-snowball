/* Estonian stemmer version 1.2

Made by Linda Freienthal in January 2019.

*/

routines (
	mark_regions
	LONGV
	case_ending
	emphasis
	plural_three_first_cases
	remove_double_kpt
	double
	undouble
	i_plural
	degrees
	substantive
	verb_exceptions
	verb
	nu
)

stringdef a" hex 'E4' //a-umlaut ä
stringdef o" hex 'F6' //o-umlaut ö
stringdef o' hex 'F5' //o with tilde õ
stringdef u" hex 'FC' //u-umlaut ü

externals ( stem )
booleans ( is_verb )
integers ( p1 )
groupings ( V1 RV KI GI)
stringescapes {}

define V1 'aeiou{o'}{a"}{o"}{u"}'
define RV 'aeiuo'
define KI 'kptgbdshfšzž'
define GI 'cjlmnqrvwxaeiou{o'}{a"}{o"}{u"}'
define mark_regions as (

    $p1 = limit

    goto V1 gopast non-V1  setmark p1
)


backwardmode (

	define emphasis as (
	    setlimit tomark p1 for ([substring])
	    test hop 4 //kingi -> kingi
            among(
		'gi' ((GI and not LONGV) delete) //jooksemegi -> jookseme, bioloogi -> bioloogi
		'ki' (('är' <-'k') or (KI delete)) //kookki -> kook, masinavärki -> masinavärk	   
	    )
	    
	)

	define verb as (
	    setlimit tomark p1 for ([substring])
            among( //nuksime, nuksin
		'nuksin' 'nuksime' 'nuksid' 'nuksite' (delete)
		'ksin' 'ksid' 'ksime' 'ksite' (delete) //conditional  'ksid'
		'mata' (delete)
		'takse' 'dakse' (delete) //impersonal: laul-dakse, luba-takse 
		'taks' 'daks' (delete) 	//impersonal conditional: laul-daks, saade-taks
		'akse' (<-'a') 		//impersonal: tulla-kse, süüa-kse, teha-kse, püüt-akse, leita-kse
		'sime' (delete) 	//pl1pst: saat-sime
		'site' (delete) 	//pl2pst: saat-site
		'sin' (delete) 		//sg1pst: laul-sin, saat-sin
		'me' (V1 delete) 	//pl1prs: laula-me, tule-me
		'da' (V1 delete) 	//da-infinitive: luba-da
		'n' (V1 delete) 	//sg1prs: kirjuta-n
		'b' (V1 delete) 	//sg3prs: laula-b
	    )
	    set is_verb
	)

	define LONGV as 
		among('aa' 'ee' 'ii' 'oo' 'uu' '{a"}{a"}' '{o"}{o"}' '{u"}{u"}' '{o'}{o'}')

	define i_plural as (
	    setlimit tomark p1 for ([substring])
	    among(
		'i' (RV) //raamatui -> raamatu, lapsikui -> lapsiku
	    )
	    delete
	)

	define case_ending as (
	    setlimit tomark p1 for ([substring])
            among(
		'sse' (RV) //illative: saapa-sse
		'st' (RV) //elative: saapa-st
		'le' (RV) //allative: raamatu-le
		'lt' (RV) //ablative: raamatu-lt
		'ga' (RV) //komitatiive: õpetaja-ga
		'ks' (RV) //translative: õpetaja-ks
		'ta' (RV) //abessive and da-infinitive: õpetaja-ta and hüpa-ta
		't' //partitiiv, raamatu-t and kapsas-t
		's'  (RV)//inessive and sg3pst: raamatu-s and sõiti-s
		'l'  (RV) //adessive: raamatu-l and kapsa-l.


/*
Terminative ending 'ni' is left out as it would worsen the results: there's more 'ni' ending words
in sg g than in ter. Same with essive 'na'.

*/
	    )
	    delete
	)


	define plural_three_first_cases as ( 
	    setlimit tomark p1 for ([substring])
            among(
		'ikkude' (<-'iku') //plural genitive: õnnelikkude -> õnneliku
		'ikke' (<-'iku') //plural partitive: rahulikke -> rahuliku
		'ike' (<-'iku') //plural genitive: ohtlike -> ohtliku
		'sid' (not LONGV delete) //plural partitive and sg2pst and pl3pst: auto-sid and laul-sid (exludes plural nominative with words like gaasid, roosid)
		'te' ((test hop 4 (not 't' delete)) or (not 't' <-'t')) //plural genitive and pl2: ministri-te, olulis-te, õun-te and saada-te, laula-te; also torte -> tort (if not in compound word) and kokkuvõtte -> kokkuvõte
		'de' (RV delete) //plural genitive: lauda-de
		'd' (RV delete) //plural nominative: voodid -> voodi, rattaid -> rattai, lapsikuid -> lapsiku
	    )	    	
	)

	define double as (
            test among('kk' 'tt' 'pp')
	)

	define undouble as (
            next [hop 1] delete
	)

	define nu as (
	    setlimit tomark p1 for ([substring])
            among(
		'nu' //haka-nu(-te-ga)
		'tu' //luba-tu(-d)
		'du' //laul-du(-te-st)
		'va' //laul-va(-te-le)
	    )
	    delete
	)

	define remove_double_kpt as (// undouble kpt consonant if 'C1C1V': mõtte(-le) -> mõte, hakka(-n) -> haka, haka(-nu-d) -> haka 
	    (V1) (double)
	    and undouble
	)

	define degrees as (
	    setlimit tomark p1 for ([substring])
            among(
		'mai' (RV) //heleda-mai(-le)
		'ma' //tugeva-ma(-le) and ma-infinitive: sõit-ma
		'm' (RV) //kauge-i-m, rõõmsa-m
	    )
	    delete
	)

	define substantive as (
	    do case_ending
	    do plural_three_first_cases
	    do degrees
	    do i_plural
	    do nu
	)
)


define verb_exceptions as (
	 [substring] atlimit
	among(//nuksin taks, nud, vat, tud, va
		'joon' 'jood' 'joob' 'joote' 'joome' 'joovad' (<-'joo')
		//'jooksin' 'jooksid' 'jooks' 'jooksite' 'jooksime' (<-'joo') kattub jooksmise minevikuga phmst
		'j{o'}in' 'j{o'}id' 'j{o'}i' 'j{o'}ime' 'j{o'}ite'  (<-'joo')
		'juuakse' 'joodakse' 'juua' 'jooma' 'joomata' (<- 'joo')
		'saan' 'saad' 'saab' 'saate' 'saame' 'saavad' (<-'saa')
		'saaksin' 'saaksid' 'saaks' 'saaksite' 'saaksime' (<-'saa')
		'sain' 'said' 'sai' 'saite' 'saime' (<-'saa')
		'saadakse' 'saadi' 'saama' 'saada' 'saamata' (<-'saa')
		'viin' 'viid' 'viib' 'viite' 'viime' 'viivad' (<-'viima')
		'viiksin' 'viiksid' 'viiks' 'viiksite' 'viiksime' (<-'viima')
		'viisin' 'viisite' 'viisime' (<-'viima')
		'viiakse' 'viidi' 'viima' 'viia' 'viimata'(<-'viima')
		'keen' 'keeb' 'keed' 'kees' 'keeme' 'keete' 'keevad' (<-'keesi')
		'keeksin' 'keeks' 'keeksid' 'keeksime' 'keeksite' (<-'keesi')
		'keema' 'keeta' 'keedakse' 'keemata' (<-'keesi')
		'l{o"}{o"}n' 'l{o"}{o"}d' 'l{o"}{o"}b' 'l{o"}{o"}me' 'l{o"}{o"}te' 'l{o"}{o"}vad'  (<-'l{o"}{o"}')
		'l{o"}{o"}ksin' 'l{o"}{o"}ksid' 'l{o"}{o"}ks' 'l{o"}{o"}ksime' 'l{o"}{o"}ksite' (<-'l{o"}{o"}')
		'l{u"}{u"}akse' 'l{o"}{o"}dakse' 'l{o"}{o"}di' 'l{o"}{o"}ma' 'l{u"}{u"}a' 'l{o"}{o"}mata'  (<-'l{o"}{o"}')
		'l{o'}in' 'l{o'}id' 'l{o'}i' 'l{o'}ime' 'l{o'}ite' (<-'l{o"}i') //looma-lõi, lööma-lõi
		'loon' 'lood' 'loob' 'loome' 'loote' 'loovad' (<-'loo')
		'looksin' 'looksid' 'looks' 'looksime' 'looksite' (<-'loo')
		'luuakse' 'loodi' 'luua' 'looma' 'loomata'(<-'loo')
		'k{a"}in' 'k{a"}ib' 'k{a"}id' 'k{a"}is'  'k{a"}ime' 'k{a"}ite' 'k{a"}ivad' (<-'k{a"}isi')
		'k{a"}iksin' 'k{a"}iks' 'k{a"}iksid' 'k{a"}iksime' 'k{a"}iksite' (<-'k{a"}isi')
		'k{a"}iakse' 'k{a"}idi' 'k{a"}ia' 'k{a"}ima' 'k{a"}imata' (<-'k{a"}isi')
		's{o"}{o"}n' 's{o"}{o"}b' 's{o"}{o"}d' 's{o"}{o"}me' 's{o"}{o"}te' 's{o"}{o"}vad' (<-'s{o"}{o"}')
		's{o"}{o"}ksin' 's{o"}{o"}ks' 's{o"}{o"}ksid' 's{o"}{o"}ksime' 's{o"}{o"}ksite' (<-'s{o"}{o"}')
		's{o'}in' 's{o'}i' 's{o'}id' 's{o'}ime' 's{o'}ite' (<-'s{o"}{o"}')
		's{u"}{u"}akse' 's{o"}{o"}dakse' 's{o"}{o"}di' 's{o"}{o"}ma' 's{o"}{o"}mata' 's{u"}{u"}a' (<-'s{o"}{o"}') 
		'toon' 'tood' 'toob' 'toote' 'toome' 'toovad' (<-'too') 
		'tooksin' 'tooksid' 'tooks' 'tooksite' 'tooksime' (<-'too') 
		't{o'}in' 't{o'}id' 't{o'}i' 't{o'}ime' 't{o'}ite' (<-'too')
		'tuuakse' 'toodi' 'tooma' 'tuua' 'toomata' (<-'too')
		'v{o'}in' 'v{o'}id' 'v{o'}ib' 'v{o'}ime' 'v{o'}is' 'v{o'}ite' 'v{o'}ivad' (<-'v{o'}isi')
		'v{o'}iksin' 'v{o'}iksid' 'v{o'}iks' 'v{o'}iksime' 'v{o'}iksite' (<-'v{o'}isi')
		'v{o'}idakse' 'v{o'}idi' 'v{o'}ida' 'v{o'}ima' 'v{o'}imata'(<-'v{o'}isi')
		'j{a"}{a"}n' 'j{a"}{a"}d' 'j{a"}{a"}b' 'j{a"}{a"}me' 'j{a"}{a"}te' 'j{a"}{a"}vad' (<-'j{a"}{a"}ma')
		'j{a"}{a"}ksin' 'j{a"}{a"}ksid' 'j{a"}{a"}ks' 'j{a"}{a"}ksime' 'j{a"}{a"}ksite' (<-'j{a"}{a"}ma')
		'j{a"}ime' 'j{a"}ite' 'j{a"}in' 'j{a"}id' 'j{a"}i' (<-'j{a"}{a"}ma')
		'j{a"}{a"}dakse' 'j{a"}{a"}da' 'j{a"}{a"}ma' 'j{a"}{a"}mata' 'j{a"}{a"}di' (<-'j{a"}{a"}ma')
		'm{u"}{u"}n' 'm{u"}{u"}d' 'm{u"}{u"}b' 'm{u"}{u"}s' 'm{u"}{u"}me' 'm{u"}{u"}te' 'm{u"}{u"}vad' (<-'m{u"}{u"}si')
		'm{u"}{u"}ksin' 'm{u"}{u"}ksid' 'm{u"}{u"}ks' 'm{u"}{u"}ksime' 'm{u"}{u"}ksite' (<-'m{u"}{u"}si')
		'm{u"}{u"}akse' 'm{u"}{u"}di' 'm{u"}{u"}a' 'm{u"}{u"}ma' 'm{u"}{u"}mata' (<-'m{u"}{u"}si')
		'loeb' 'loen' 'loed' 'loeme' 'loete' 'loevad' (<- 'luge')
		'loeks' 'loeksin' 'loeksid' 'loeksime' 'loeksite' (<- 'luge')
		'p{o'}en' 'p{o'}eb' 'p{o'}ed' 'p{o'}eme' 'p{o'}ete' 'p{o'}evad' (<- 'p{o'}de')
		'p{o'}eksin' 'p{o'}eks' 'p{o'}eksid' 'p{o'}eksime' 'p{o'}eksite' (<- 'p{o'}de')
		'laon' 'laob' 'laod' 'laome' 'laote' 'laovad' (<- 'ladu')
		'laoksin' 'laoks' 'laoksid' 'laoksime' 'laoksite' (<- 'ladu')
		'teeksin' 'teeks' 'teeksid' 'teeksime' 'teeksite' (<- 'tegema') //(<- 'tegi')
		'teen' 'teeb' 'teed' 'teeme' 'teete' 'teevad' (<- 'tegema')
		'tegin' 'tegi' 'tegid' 'tegime' 'tegite' (<-'tegema')
		'tehakse' 'tehti' 'tegema' 'tegemata' 'teha' (<-'tegema')
		'n{a"}en' 'n{a"}eb' 'n{a"}ed' 'n{a"}eme' 'n{a"}ete' 'n{a"}evad' (<-'n{a"}gi')
		'n{a"}eksin' 'n{a"}eks' 'n{a"}eksid' 'n{a"}eksime' 'n{a"}eksite' (<-'n{a"}gi')
		//'n{a"}gin' 'n{a"}gid' 'n{a"}gi' 'n{a"}gime' 'n{a"}gite' 
		'n{a"}hakse' 'n{a"}hti' 'n{a"}ha' 'n{a"}gema' 'n{a"}gemata' (<-'n{a"}gi')
//lugema-tüüpi (ÕS 57. tüüp) kuulub ligi 20 /2 I V/-sõna, mille esimese ja teise silbi piiril on lühike klusiil, nagu püga/ma, sada/ma, põde/ma, ladu/ma, kudu/ma. Nee
	)
)


define stem as (
	do mark_regions
	not verb_exceptions
	unset is_verb
	backwards (
	    do emphasis
	    do verb
	    try (not is_verb do substantive)
	    do remove_double_kpt
	    
	)
)
